
plugins {
    alias(libs.plugins.detekt)
    alias(libs.plugins.dokka)
    id 'java-gradle-plugin'
    id 'java-test-fixtures'
    alias(libs.plugins.jfrog)
    alias(libs.plugins.kotlin)
    id 'maven-publish'
    alias(libs.plugins.pitest)
    id 'version-catalog'
}

group = "com.tpero.gradle"

publishToMavenLocal {
    dependsOn build
}

repositories {
    mavenCentral()
    google()
    maven {
        url = 'https://plugins.gradle.org/m2/'
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

dependencies {
    implementation libs.commons.lang
    implementation libs.bundles.spring

    // Dependencies needed for applying other plugins
    implementation libs.detekt
    implementation libs.dokka
    implementation libs.itest
    implementation libs.kotlin.allopen
    implementation libs.kotlin.gradle.plugin
    implementation libs.pitest
    implementation libs.spring.boot.plugin
    implementation libs.spring.dependency.mgmt
    implementation libs.android.gradle
    implementation libs.sonar
    implementation libs.gatling

    testImplementation libs.bundles.test
}

group = 'com.tpero.gradle'
version = '0.0'

catalog {
    versionCatalog {
        from(files('gradle/libs.versions.toml'))
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.versionCatalog
        }
    }
}

gradlePlugin {
    plugins {
        register('jvm') {
            id = 'jvm'
            version = 0.0
            implementationClass = 'com.tpero.gradle.jvm.JvmPlugin'
        }

        register('jvm-quality') {
            id = 'jvm-quality'
            version = 0.0
            implementationClass = 'com.tpero.gradle.jvm.JvmQualityPlugin'
        }

        register('android') {
            id = 'android'
            version = 0.0
            implementationClass = 'com.tpero.gradle.jvm.AndroidPlugin'
        }

        register('jvm-lib') {
            id = 'jvm-lib'
            version = 0.0
            implementationClass = 'com.tpero.gradle.jvm.JvmLibPlugin'
        }

        register('spring') {
            id = 'spring'
            version = 0.0
            implementationClass = 'com.tpero.gradle.jvm.SpringPlugin'
        }

        register('spring-quality') {
            id = 'spring-quality'
            version = 0.0
            implementationClass = 'com.tpero.gradle.jvm.SpringQualityPlugin'
        }
    }
}

pitest {
    testPlugin.set('KotlinTest')
    targetClasses.set(['com.tpero.gradle.*'])
    outputFormats.set(['HTML', 'XML'])
    timestampedReports.set(false)
    enableDefaultIncrementalAnalysis.set(true)
    useClasspathFile.set(true)
}

artifactory {
    contextUrl = System.properties['artifactoryUrl']

    publish {
        repository {
            repoKey = 'default-maven-local'
            username = System.properties['artifactoryUser']
            password = System.properties['artifactoryApiKey']
            maven = true
        }

        defaults {
            publications(publishing.publications.names.toArray())
        }
    }
}
